export { iterate, next, count, nth, Range, range, Mapping, mapping }

bundle IndexIterator(xs, i: Int64)

fn IndexIterator(xs) { IndexIterator(xs, 1) }

fn iterate(i: IndexIterator) { i }

fn next(&itr: IndexIterator(xs, i)) {
  if i > length(xs) {
    return nil
  } else {
    val = xs[i]
    i = i + 1
    itr = IndexIterator(xs, i)
    return Some(val)
  }
}

fn count(itr) {
  i = 0
  for _ = itr { i = i + 1 }
  return i
}

fn nth(xs, n) {
  i = 1
  for x = xs {
    if i == n { return x }
    i = i + 1
  }
  abort("Index out of range")
}

bundle Range(start, stop)

fn range(start, stop) { Range(start, stop) }

fn length(Range(start, stop)) { (stop-start)+1 }

fn Range(start, stop)[i] { (i - 1) + start }

fn iterate(xs: Range) { IndexIterator(xs) }

bundle Mapping(itr, func)

fn iterate(m: Mapping) { m }

fn next(&m: Mapping(itr, func)) {
  next = next(&itr)
  m = Mapping(itr, func)
  if let Some(x) = next { Some(func(x)) }
}

fn mapping(itr, func) { Mapping(iterate(itr), func) }
