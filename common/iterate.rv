export { next, iterator, Range, range, Mapping, mapping }

bundle IndexIterator(xs, i: Int64)

fn IndexIterator(xs) { IndexIterator(xs, 1) }

fn iterator(i: IndexIterator) { i }

fn next(&itr: IndexIterator(xs, i)) {
  if i > length(xs) {
    return nil
  } else {
    val = xs[i]
    i = i + 1
    itr = IndexIterator(xs, i)
    return Some(val)
  }
}

bundle Range(start, stop)

fn range(start, stop) { Range(start, stop) }

fn length(Range(start, stop)) { (stop-start)+1 }

fn Range(start, stop)[i] { (i - 1) + start }

fn iterator(xs: Range) { IndexIterator(xs) }

bundle Mapping(func, itr)

fn iterator(m: Mapping) { m }

fn next(&m: Mapping(func, itr)) {
  next = next(&itr)
  m = Mapping(func, itr)
  # TODO if let / match
  if nil?(next) {
    return nil
  } else {
    Some(x) = notnil(next)
    return Some(func(x))
  }
}

fn mapping(func, itr) { Mapping(func, iterator(itr)) }
