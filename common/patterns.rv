export { Any, NotNil, Hole, Literal, Trait, Pack, Bind, Constructor, And
         match, matchTrait, ismatch, isa }

Any = tag".Any"

fn matchTrait(tag".Any", x) { Some(x) }

NotNil = tag".NotNil"

fn matchTrait(tag".NotNil", x) {
  if not(nil?(x)) { Some(notnil(x)) }
}

bundle Params(T, Ts)

fn show(pack(tag".Params", T, Ts...)) {
  show(T)
  show(Ts)
}

bundle Pattern {
  Hole()
  Literal(x)
  Trait(T)
  Pack(xs...)
  Bind(name, pat)
  Constructor(name, args...)
  And(p, q)
}

constructorPattern = tag".constructorPattern"

fn _match(val, pack(tag"common.Hole")) {
  return [val, simpledict()]
}

fn _match(val, pack(tag"common.Literal", pat)) {
  (val == pat) || return
  return [pat, simpledict()]
}

fn _match(val, pack(tag"common.Trait", T)) {
  m = matchTrait(T, val)
  nil?(m) && return
  return [part(notnil(m), 1), simpledict()]
}

fn _match(val, pack(tag"common.Bind", name, p)) {
  m = _match(val, p)
  nil?(m) && return
  m = notnil(m)
  val = part(m, 1)
  bs = part(m, 2)
  setkey(&bs, name, val)
  return [val, bs]
}

fn _match(xs, p: Pack) {
  eq(nparts(xs), sub(nparts(p), 1)) || return
  m = _match(tag(xs), part(p, 1))
  nil?(m) && return
  m = notnil(m)
  tag = part(m, 1)
  xs = tagcast(xs, tag)
  bs = part(m, 2)
  result = pack(tag)
  i = 1
  while lt(i, nparts(p)) {
    if not(nil?(bs)) {
      m = _match(part(xs, i), part(p, add(i, 1)))
      if nil?(m) {
        bs = nil
        result = nil
      } else {
        m = notnil(m)
        result = packcat(notnil(result), [part(m, 1)])
        bs = merge(notnil(bs), notnil(part(m, 2)))
      }
    } else {
      # if the above is certain to fail, make sure the whole match is
      bs = nil
      result = nil
    }
    i = add(i, 1)
  }
  nil?(bs) && return
  return [notnil(result), notnil(bs)]
}

fn _match(val, pack(tag".Constructor", name, args...)) {
  _match(val, constructorPattern(name, args...))
}

fn match(val, pat) {
  result = _match(val, pat)
  nil?(result) && return
  return part(notnil(result), 2)
}

fn isa(val, T) {
  not(nil?(matchTrait(T, val)))
}
