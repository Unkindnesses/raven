export {
  Bits, Int32, Int64, Float32, Float64, Complex, Integer
  ==, !=, +, -, *, /, ^, >, <, >=, <=, &, abs2, div, rem, not
  zero, one, round
}

fn matchTrait(tag"common.core.Bits", x: pack(tag"common.core.Bits", _)) { Some(x) }
fn matchTrait(tag"common.core.Int32", x: pack(tag"common.core.Int32", _)) { Some(x) }
fn matchTrait(tag"common.core.Int64", x: pack(tag"common.core.Int64", _)) { Some(x) }
fn matchTrait(tag"common.core.Float32", x: pack(tag"common.core.Float32", _)) { Some(x) }
fn matchTrait(tag"common.core.Float64", x: pack(tag"common.core.Float64", _)) { Some(x) }

Integer = tag".Integer"

fn matchTrait(tag".Integer", x: (Int32 | Int64)) { Some(x) }

include "numbers/complex.rv"

extend fn Int64(x: Int64) { x }

fn zero(x: Int64) { Int64(0) }
fn zero(x: Int32) { Int32(0) }

fn one(x: Int64) { Int64(1) }
fn one(x: Int32) { Int32(1) }

fn a != b {
  not(a == b)
}

fn x ^ (n: Integer) {
  r = one(x)
  while n > 0 {
    n = n - one(n)
    r = r * x
  }
  return r
}

fn show(x: Bits) {
  print("bits\"")
  N = bitsize(x)
  for i in range(1, N) {
    print(Int64(bitand(bitshr(x, bitcast(x, N-i)), bitcast(x, 1))))
  }
  print("\"")
}

fn print(x: (Int64 | Int32 | Float64 | Float32)) {
  print(string(x))
}

fn show(x: (Int64 | Float64)) {
  print(x)
}

fn show(x: Int32) {
  print("Int32(")
  print(x)
  print(")")
}

fn show(x: Float32) {
  print("Float32(")
  print(x)
  print(")")
}
