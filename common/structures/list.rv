export { List, length, append, isempty, map, collect }

bundle List(xs...)

fn list(xs...) { List(xs...) }

fn length(xs: List) {
  nparts(xs)
}

fn (xs: List)[i] {
  part(xs, i)
}

fn isempty(xs: List) {
  length(xs) == 0
}

fn append(&xs, x) {
  xs = [xs..., x]
  return xs
}

fn iterator(xs: List) { IndexIterator(xs) }

fn rest(xs: List) {
  if isempty(xs) { abort("List is empty")  }
  result = []
  for i in range(2, length(xs)) {
    append(&result, xs[i])
  }
  return result
}

fn show(x: List) {
  print("[")
  for i in range(1, nparts(x)) {
    if i != 1 {
      print(", ")
    }
    show(x[i])
    i = i + 1
  }
  print("]")
}

fn map(f, xs) {
  ys = []
  for x in xs {
    append(&ys, f(x))
  }
  return ys
}

fn collect(xs) { map(identity, xs) }

fn js(xs: List) {
  ys = js().Array.of()
  for x in xs { ys.push(x) }
  return ys
}
