export {
  Sequence, Prepend, Empty, seq, first, rest, prepend, isempty, length, seqRange, repeat, sum
}

bundle Sequence { Prepend(xs, x), Empty() }

fn prepend(xs: Sequence, x) { Prepend(xs, x) }

fn seq() { Empty() }
fn seq(x, xs...) { prepend(seq(xs...), x) }

fn first(l: Empty) { abort("first: seq is empty") }
fn first(l: Prepend) { part(l, 2) }

fn rest(l: Empty)  { abort("rest: seq is empty") }
fn rest(l: Prepend) { part(l, 1) }

fn isempty(_: Prepend) { false }
fn isempty(_: Empty) { true }

fn length(Prepend(xs, _)) { 1 + length(xs) }
fn length(Empty()) { 0 }

fn seqRange(start, end) {
  if start > end {
    seq()
  } else {
    prepend(seqRange(start+one(start), end), start)
  }
}

fn repeat(n, x) { prepend(repeat(n-1, x), x) }
fn repeat(0, x) { seq() }

fn sum(Empty(), total) { total }
fn sum(Prepend(xs, x), total) { sum(xs, total + x) }

fn sum(Prepend(xs, x)) { sum(xs, x) }

fn printList(Empty()) {
  print(")")
}

fn printList(Prepend(xs, x)) {
  print(x)
  if not(isa(xs, Empty)) {
    print(", ")
  }
  printList(xs)
}

fn show(xs: Sequence) {
  print("seq(")
  printList(xs)
}
