export {
  JSObject, debugger, abort, js, call, errcall, await, new, sleep
  readline, require
}

fn matchTrait(tag"common.core.Ref", x: pack(tag"common.core.Ref", _)) { Some(x) }

fn show(x: Ref) { print("Ref(...)") }

bundle JSObject(ref: Ref)

fn JSObject() { new(js().Object) }

fn js(x: JSObject) { x }

fn jsref(JSObject(ref)) { ref }

fn js() {
  ref = wasm call(support.global): ref
  return JSObject(ref)
}

fn (obj: JSObject)[prop] { obj[js(prop)] }

fn (obj: JSObject)[prop: JSObject] {
  ref = wasm call(support.get, jsref(obj): ref, jsref(prop): ref): ref
  return JSObject(ref)
}

fn set(&obj: JSObject, x, prop) { obj[js(prop)] = js(x) }

fn set(&obj: JSObject, x: JSObject, prop: JSObject) {
  wasm call(support.set, jsref(obj): ref, jsref(prop): ref, jsref(x): ref): ref
  return obj
}

fn field(obj: JSObject, f: Tag) { obj[string(f)] }

fn call(obj: JSObject, meth, args...) { apply(obj, js(meth), js(args)) }

fn apply(obj: JSObject, meth: JSObject, args: JSObject) {
  ref = wasm call(support.apply, jsref(obj): ref, jsref(meth): ref, jsref(args): ref): ref
  return JSObject(ref)
}

fn call(obj: JSObject, meth) { call(obj, js(meth)) }

fn call(obj: JSObject, meth: JSObject) {
  ref = wasm call(support.call, jsref(obj): ref, jsref(meth): ref): ref
  return JSObject(ref)
}

fn call(obj: JSObject, meth, x) { call(obj, js(meth), js(x)) }

fn call(obj: JSObject, meth: JSObject, x: JSObject) {
  ref = wasm call(support.call, jsref(obj): ref, jsref(meth): ref, jsref(x): ref): ref
  return JSObject(ref)
}

fn method(obj: JSObject, meth: Tag, args...) {
  call(obj, string(meth), args...)
}

fn errcall(obj: JSObject, meth) { errcall(obj, js(meth)) }

fn errcall(obj: JSObject, meth: JSObject) {
  [err, ref] = wasm call(support.errcall, jsref(obj): ref, jsref(meth): ref): (i32, ref)
  result = JSObject(ref)
  if Bool(err) {
    return Err(result)
  } else {
    return Ok(result)
  }
}

fn errcall(obj: JSObject, meth, x) { errcall(obj, js(meth), js(x)) }

fn errcall(obj: JSObject, meth: JSObject, x: JSObject) {
  [err, ref] = wasm call(support.errcall, jsref(obj): ref, jsref(meth): ref, jsref(x): ref): (i32, ref)
  result = JSObject(ref)
  if Bool(err) {
    return Err(result)
  } else {
    return Ok(result)
  }
}

fn await(obj: JSObject) {
  ref = wasm call(support.await, jsref(obj): ref): ref
  return JSObject(ref)
}

fn (a: JSObject) == (b: JSObject) {
  result = wasm call(support.equal, jsref(a): ref, jsref(b): ref): i32
  return Bool(result)
}

fn new(T: JSObject, args...) {
  js().Reflect.construct(T, args)
}

fn js(x: Float64) {
  ref = wasm call(support.identity, x: f64): ref
  return JSObject(ref)
}

fn js(x: Float32) { js(Float64(x)) }

@extend
fn Float64(obj: JSObject) {
  x = wasm call(support.identity, jsref(obj): ref): f64
  return x
}

@extend
fn Int32(obj: JSObject) {
  x = wasm call(support.identity, jsref(obj): ref): i32
  return Int32(x)
}

fn Bool(obj: JSObject) { Bool(Int32(obj)) }

fn condition(obj: JSObject) { condition(Bool(obj)) }
fn not(obj: JSObject) { not(Bool(obj)) }

fn debugger() {
  wasm call(support.debugger)
}

fn abort(s: JSObject) {
  wasm call(support.abort, jsref(s): ref): unreachable
}

fn abort(s: JSObject, cause: JSObject) {
  wasm call(support.abort, jsref(s): ref, jsref(cause): ref): unreachable
}

fn abort(s: String) { abort(js(s)) }
fn abort(s: String, cause) { abort(js(s), js(cause)) }

fn string(x: JSObject) {
  String(x.toString())
}

fn string(x: (Float32 | Float64)) {
  string(js(x))
}

fn print(x: String) {
  js().process.stdout.write(x)
  return
}

fn show(x: JSObject) {
  print(string(x))
}

fn println(x: JSObject) {
  js().console.log(x)
  return
}

fn replshow(x: JSObject) {
  println(x)
}

fn iterator(x: JSObject) {
  x[js().Symbol.iterator].call(x)
}

fn next(&x: JSObject) {
  val = x.next()
  if not(val.done) { Some(val.value) }
}

fn require(s) {
  return js().require(s)
}

fn sleep(n) {
  await(js().sleep(n))
  return
}

fn readline() {
  return String(await(js().readline()))
}
