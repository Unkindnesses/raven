export {
  JSObject, abort, js, property, call, errcall, await, sleep
  readline, require
}

fn jsfree(ptr: Ptr) {
  id = i32load(ptr)
  wasm { call(support.release, id: i32) }
  return
}

bundle JSObject(ref: Ref)

fn JSObject(id: Int32) {
  if jsalloc() {
    r = Ref(8)
    i32store!(pointer(r), id)
    cleanup!(r, jsfree)
    return JSObject(r)
  } else {
    return pack(tag".JSObject", id)
  }
}

fn js(x: JSObject) { x }

fn jsref(x) { jsref(js(x)) }

fn jsref(JSObject(ref: Ref)) { i32load(pointer(ref)) }

# We have a compiler option which skips the `Ref` wrapper, which lets us avoid
# compiling malloc. (And of course JS objects won't be freed.)
fn jsref(JSObject(ref: Int32)) { ref }

fn retain!(JSObject(ref: Ref)) { retain!(ref) }

fn retain!(JSObject(_: Int32)) {}

fn js() {
  ref = wasm { call(support.global): i32 }
  return JSObject(ref)
}

fn (obj: JSObject)[prop] {
  prop = js(prop)
  ref = wasm { call(support.property, jsref(obj): i32, jsref(prop): i32): i32 }
  hold(obj, prop)
  return JSObject(ref)
}

fn field(obj: JSObject, f: Tag) { obj[string(f)] }

fn call(obj, meth) { call(js(obj), js(meth)) }

fn call(obj: JSObject, meth: JSObject) {
  ref = wasm { call(support.call, jsref(obj): i32, jsref(meth): i32): i32 }
  hold(obj, meth)
  return JSObject(ref)
}

fn call(obj, meth, x) { call(js(obj), js(meth), js(x)) }

fn call(obj: JSObject, meth: JSObject, x: JSObject) {
  ref = wasm { call(support.call, jsref(obj): i32, jsref(meth): i32, jsref(x): i32): i32 }
  hold(obj, meth, x)
  return JSObject(ref)
}

fn method(obj: JSObject, meth: Tag, args...) {
  call(obj, string(meth), args...)
}

fn errcall(obj, meth) { errcall(js(obj), js(meth)) }

fn errcall(obj: JSObject, meth: JSObject) {
  [err, ref] = wasm { call(support.errcall, jsref(obj): i32, jsref(meth): i32): (i32, i32) }
  hold(obj, meth)
  result = JSObject(ref)
  if err {
    return Err(result)
  } else {
    return Ok(result)
  }
}

fn errcall(obj, meth, x) { errcall(js(obj), js(meth), js(x)) }

fn errcall(obj: JSObject, meth: JSObject, x: JSObject) {
  [err, ref] = wasm { call(support.errcall, jsref(obj): i32, jsref(meth): i32, jsref(x): i32): (i32, i32) }
  hold(obj, meth, x)
  result = JSObject(ref)
  if err {
    return Err(result)
  } else {
    return Ok(result)
  }
}

fn await(obj: JSObject) {
  ref = wasm { call(support.await, jsref(obj): i32): i32 }
  hold(obj)
  return JSObject(ref)
}

fn equal(a: JSObject, b: JSObject) {
  result = wasm { call(support.equal, jsref(a): i32, jsref(b): i32): i32 }
  hold(a, b)
  return result
}

fn new(T: JSObject) {
  this = js().Object.create(T.prototype)
  T.call(this)
  return this
}

fn js(x: Float64) {
  ref = wasm { call(support.createRef, x: f64): i32 }
  return JSObject(ref)
}

fn js(x: Int64) { js(Float64(x)) }
fn js(x: Int32) { js(Float64(x)) }

extend fn Float64(obj: JSObject) {
  x = wasm { call(support.fromRef, jsref(obj): i32): f64 }
  hold(obj)
  return x
}

extend fn Int32(obj: JSObject) {
  x = wasm { call(support.fromRef, jsref(obj): i32): i32 }
  hold(obj)
  return x
}

fn abort(s: JSObject) {
  retain!(s)
  wasm { call(support.abort, jsref(s): i32): unreachable }
}

fn abort(s: JSObject, cause: JSObject) {
  retain!(s)
  retain!(cause)
  wasm { call(support.abort, jsref(s): i32, jsref(cause): i32): unreachable }
}

fn abort(s: String) { abort(js(s)) }
fn abort(s: String, cause) { abort(js(s), js(cause)) }

fn string(x: JSObject) {
  String(x.toString())
}

fn string(x: (Int32 | Int64 | Float32 | Float64)) {
  string(js(x))
}

fn print(x: String) {
  js().process.stdout.write(x)
  return
}

fn show(x: JSObject) {
  print(string(x))
}

fn println(x: JSObject) {
  js().console.log(x)
  return
}

fn replshow(x: JSObject) {
  println(x)
}

fn iterator(x: JSObject) {
  x[js().Symbol.iterator].call(x)
}

fn iterate(&x: JSObject) {
  val = x.next()
  if Int32(val.done) {
    return nil
  } else {
    return Some(val.value)
  }
}

fn require(s) {
  return js().require(s)
}

fn sleep(n) {
  await(js().sleep(n))
  return
}

fn readline() {
  return String(await(js().readline()))
}
