export { Ptr, Function, Ref, addr, pointer, invoke }

bundle Ptr(x: Int32)

fn Ptr(x: Int64) { Ptr(Int32(x)) }

fn addr(Ptr(x)) { Int32(x) }

fn show(x: Ptr) {
  print("Ptr@")
  print(addr(x))
}

fn (p: Ptr) + x {
  Ptr(addr(p) + Int32(x))
}

fn (p: Ptr) - x {
  Ptr(addr(p) - Int32(x))
}

fn (p: Ptr) == (q: Ptr) {
  addr(p) == addr(q)
}

fn memoryPages() {
  Int32(wasm memory.size(): i32)
}

fn memoryGrow!(n: Int32) {
  Int32(wasm memory.grow(bits(n): i32): i32)
}

fn i32load(p: Ptr) {
  a = addr(p)
  Int32(wasm i32.load(bits(a): i32): i32)
}

fn i32store!(p: Ptr, x: Int32) {
  a = addr(p)
  wasm i32.store(bits(a): i32, bits(x): i32)
}

# Function pointer

TPtr = Pack(Literal(Ptr), Pack(Literal(Int), bits 32))
TNil = Pack(Literal(Nil))

fn Function(f, I, O) {
  ptr = function(f, I, O)
  Function(ptr, I, O)
}

bundle Function(f: Int32, I, O)

@extend
fn invoke(f: Function, xs...) {
  Function(f, I, O) = f
  invoke(f, I, O, xs...)
}
