data Pattern {
  Literal(x)
  Data(xs...)
  Bind(name)
  Constructor(name, args...)
}

fn match(val, data(`Literal`, pat)) {
  if (pat == val) {
    return simpledict()
  } else {
    return nil
  }
}

fn match(val, data(`Bind`, name)) {
  bs = simpledict()
  setkey(&bs, name, val)
  return bs
}

fn match(data(), data(`Data`)) { simpledict() }

fn match(data(x, xs...), data(`Data`, p, ps...)) {
  bs = match(x, p)
  isnil(bs) && return
  cs = match(data(xs...), Data(ps...))
  isnil(cs) && return nil
  merge(notnil(bs), notnil(cs))
}

fn match(val, data(`Constructor`, name, args...)) {
  match(val, constructorPattern(name, args...))
}

fn ismatch(val, pat) {
  not(isnil(match(val, pat)))
}
