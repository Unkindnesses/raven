data Pattern {
  Hole()
  Literal(x)
  Isa(T)
  Data(xs...)
  Bind(name)
  Constructor(name, args...)
  And(p, q)
}

fn match(val, data(`Hole`)) {
  return simpledict()
}

fn match(val, data(`Literal`, pat)) {
  if (pat == val) {
    return simpledict()
  } else {
    return
  }
}

fn match(val, data(`Isa`, pat)) {
  if isa(val, pat) {
    return simpledict()
  } else {
    return
  }
}

fn match(val, data(`Bind`, name)) {
  bs = simpledict()
  setkey(&bs, name, val)
  return bs
}

fn match(data(), data(`Data`)) { simpledict() }

fn match(data(x, xs...), data(`Data`, p, ps...)) {
  bs = match(x, p)
  isnil(bs) && return
  cs = match(data(xs...), Data(ps...))
  isnil(cs) && return
  merge(notnil(bs), notnil(cs))
}

fn match(val, data(`Constructor`, name, args...)) {
  match(val, constructorPattern(name, args...))
}

fn match(val, data(`And`, p, q)) {
  bs = match(val, p)
  isnil(bs) && return
  cs = match(val, q)
  isnil(cs) && return
  merge(notnil(bs), notnil(cs))
}

fn ismatch(val, pat) {
  not(isnil(match(val, pat)))
}
