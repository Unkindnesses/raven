bundle Sequence { Prepend(xs, x) | Empty() }

fn prepend(xs: Sequence, x) { Prepend(xs, x) }
fn prepend(xs: Sequence, x, y) { prepend(prepend(xs, x), y) }

fn first(l: Empty) { panic("first: list is empty") }
fn first(l: Prepend) { part(l, 2) }

fn rest(l: Empty)  { panic("rest: list is empty") }
fn rest(l: Prepend) { part(l, 1) }

fn list() { Empty() }
fn list(x, xs...) { prepend(list(xs...), x) }

fn empty(xs: Prepend) { false }
fn empty(xs: Empty) { true }

fn length(xs: Prepend) { 1 + length(rest(xs)) }
fn length(xs: Empty) { 0 }

fn lrange(start, end) {
  if (start > end) {
    list()
  } else {
    prepend(range(start+one(start), end), start)
  }
}

fn repeat(n, x) { prepend(repeat(n-1, x), x) }
fn repeat(0, x) { list() }

fn interleave(xs: Sequence, ys: Sequence) { list() }

fn interleave(xs: Prepend, ys: Prepend) {
  prepend(interleave(rest(xs), rest(ys)), first(ys), first(xs))
}

fn interpose(sep, xs: Sequence) {
  rest(interleave(repeat(length(xs), sep), xs))
}

fn interpose(sep, xs: Empty) { xs }

fn sum(xs: Prepend, s) { sum(rest(xs), s + first(xs)) }
fn sum(xs: Empty, s) { s }

fn sum(xs: Sequence) { sum(xs, zero(first(xs))) }

fn map(f, xs: Empty) { xs }
fn map(f, xs: Prepend) { prepend(map(f, rest(xs)), f(first(xs))) }

fn foreach(f, _: Empty) { nil }
fn foreach(f, xs: Prepend) {
  f(first(xs))
  foreach(f, rest(xs))
}

# fn print(xs: Sequence) {
#   print("list(")
#   foreach(print, interpose(", ", xs))
#   print(")")
# }

fn printList(xs: Empty) {
  print(")")
}

fn printList(xs: Prepend) {
  print(first(xs))
  if not(isa(rest(xs), Empty)) {
    print(", ")
  }
  printList(rest(xs))
}

fn print(xs: Sequence) {
  print("list(")
  printList(xs)
}
