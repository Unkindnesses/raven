List = `List`

fn Empty() { data(`Empty`) }
fn Prepend(xs, x) { data(`Prepend`, xs, x) }

fn isa(data(`Empty`), `Empty`) { true }
fn isa(data(`Prepend`, _, _), `Prepend`) { true }
fn isa(_: Prepend | Empty, `List`) { true }

fn prepend(xs: List, x) { Prepend(xs, x) }
fn prepend(xs: List, x, y) { prepend(prepend(xs, x), y) }

fn first(l: Empty) { panic("first: List is empty") }
fn first(l: Prepend) { part(l, 2) }

fn rest(l: Empty)  { panic("rest: List is empty") }
fn rest(l: Prepend) { part(l, 1) }

fn list() { Empty() }
fn list(x, xs...) { prepend(list(xs...), x) }

fn empty?(xs: Prepend) { false }
fn empty?(xs: Empty) { true }

fn length(xs: Prepend) { 1 + length(rest(xs)) }
fn length(xs: Empty) { 0 }

fn range(start, end) {
  if (start > end) {
    list()
  } else {
    prepend(range(start+one(start), end), start)
  }
}

fn repeat(n, x) { prepend(repeat(n-1, x), x) }
fn repeat(0, x) { list() }

fn interleave(xs: List, ys: List) { list() }

fn interleave(xs: Prepend, ys: Prepend) {
  prepend(interleave(rest(xs), rest(ys)), first(ys), first(xs))
}

fn interpose(sep, xs: List) {
  rest(interleave(repeat(length(xs), sep), xs))
}

fn interpose(sep, xs: Empty) { xs }

fn sum(xs: Prepend, s) { sum(rest(xs), s + first(xs)) }
fn sum(xs: Empty, s) { s }

fn sum(xs: List) { sum(xs, zero(first(xs))) }

fn map(f, xs: Empty) { xs }
fn map(f, xs: Prepend) { prepend(map(f, rest(xs)), f(first(xs))) }

fn foreach(f, _: Empty) { nil }
fn foreach(f, xs: Prepend) {
  f(first(xs))
  foreach(f, rest(xs))
}

fn print(xs: List) {
  print("list(")
  foreach(print, interpose(", ", xs))
  print(")")
}
